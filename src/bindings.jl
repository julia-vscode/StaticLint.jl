mutable struct ImportBinding
    loc::Location
    si::SIndex
    val::Union{CSTParser.AbstractEXPR,Dict,SymbolServer.SymStore}
    refs::Vector{Reference}
    ImportBinding(loc, si, val, refs = Reference[]) = new(loc, si, val, refs)
end

mutable struct Binding
    loc::Location
    si::SIndex
    val::Union{CSTParser.AbstractEXPR,Dict,SymbolServer.SymStore}
    t::Union{Nothing,Dict,CSTParser.AbstractEXPR,SymbolServer.SymStore}
    refs::Vector{Reference}
end
Binding(loc, si, val, t= nothing) = Binding(loc, si, val, t, Reference[])

function add_binding(name, x, state, s, t = nothing)
    # global bindinglist
    s.bindings += 1
    val = Binding(Location(state), SIndex(s.index, s.bindings), x, t)
    
    add_binding(name, val, state.bindings, s.index)
end

function add_binding(name, binding::Binding, bindings::Dict, index::Tuple)
    if haskey(bindings, index)
        if haskey(bindings[index], name)
            # Don't repeat bindings (occurs with for/generator loops)
            if last(bindings[index][name]).loc.offset == binding.loc.offset && last(bindings[index][name]).loc.file == binding.loc.file
                return
            end
            push!(bindings[index][name], binding)
        else
            bindings[index][name] = Binding[binding]
        end
    else
        bindings[index] = Dict{String,Vector{Binding}}(name => Binding[binding])
    end
end
    

# Gets bindings added to the current scope which an expression is in: modules, functions, macros, datatype declarations, variables and imported bindings.
function ext_binding(x, state, s)
    if CSTParser.defines_module(x)
        name = CSTParser.str_value(CSTParser.get_name(x))
        add_binding(name, x, state, s, state.server.packages["Core"].vals["Module"])
    elseif CSTParser.defines_function(x)
        name = CSTParser.str_value(CSTParser.get_name(x))
        add_binding(name, x, state, s, state.server.packages["Core"].vals["Function"])
    elseif CSTParser.defines_macro(x)
        name = string("@", CSTParser.str_value(CSTParser.get_name(x)))
        add_binding(name, x, state, s, nothing)
    elseif CSTParser.defines_datatype(x)           
        name = CSTParser.str_value(CSTParser.get_name(x))
        add_binding(name, x, state, s, state.server.packages["Core"].vals["DataType"])
    elseif CSTParser.is_assignment(x)
        ass = x.arg1
        ass = CSTParser.rem_decl(ass)
        if ass isa CSTParser.EXPR{CSTParser.Curly}
            #typealias
            ass = CSTParser.rem_curly(ass)
            add_binding(CSTParser.str_value(ass), x, state, s)
        else
            assign_to_tuple(ass, x, state.loc.offset, state, s)
        end
    # elseif x isa CSTParser.EXPR{CSTParser.Using} || x isa CSTParser.EXPR{CSTParser.Import} || x isa CSTParser.EXPR{CSTParser.ImportAll}
    #     get_imports(x, state, s)
    elseif x isa CSTParser.EXPR{CSTParser.Export}
        add_export_bindings(x, state, s)
    elseif x isa CSTParser.EXPR{CSTParser.MacroCall} && x.args[1] isa CSTParser.EXPR{CSTParser.MacroName} && length(x.args[1].args) > 1
        # Special case for bindings introduces by Base macro.
        # Needs fix to ensure macro label is pointing to Base.@enum, etc.
        if CSTParser.str_value(x.args[1].args[2]) == "enum"
            for i = 2:length(x.args)
                if x.args[i] isa CSTParser.IDENTIFIER
                    name = CSTParser.str_value(x.args[i])
                    add_binding(name, x, state, s)
                end
            end
        elseif CSTParser.str_value(x.args[1].args[2]) == "label"
            if length(x.args) > 1 && x.args[2] isa CSTParser.IDENTIFIER
                add_binding(CSTParser.str_value(x.args[2]), x, state, s)
            end
        end
    end
end

function add_export_bindings(x, state, s)
    if !haskey(state.exports, s.index)
        state.exports[s.index] = []
    end
    for i = 2:length(x.args)
        arg = x.args[i]
        !(arg isa CSTParser.IDENTIFIER) && continue
        push!(state.exports[s.index], CSTParser.str_value(arg))
    end
end

# Gets bindings that are created inside the scope generated by an expression.
function int_binding(x, state, s)
    if CSTParser.defines_module(x)
        name = CSTParser.str_value(CSTParser.get_name(x))
        add_binding(name, x, state, s, state.server.packages["Core"].vals["Module"])
    elseif CSTParser.defines_function(x) || CSTParser.defines_macro(x)
        get_fcall_bindings(CSTParser.get_sig(x), state, s)
    elseif CSTParser.defines_datatype(x)
        if x isa CSTParser.EXPR{CSTParser.Struct} || x isa CSTParser.EXPR{CSTParser.Mutable}
            get_struct_bindings(x, state, s)
        elseif x isa CSTParser.EXPR{CSTParser.Abstract}
            sig = CSTParser.get_sig(x)
            sig = CSTParser.rem_subtype(sig)
            sig = CSTParser.rem_where(sig)
            for arg in CSTParser.get_curly_params(sig)
                add_binding(arg, x, state, s, state.server.packages["Core"].vals["DataType"])
            end
        end
    elseif x isa CSTParser.EXPR{CSTParser.For}
        if is_for_iter(x.args[2])
            assign_to_tuple(x.args[2].arg1, x.args[2], state.loc.offset +x.args[1].fullspan, state, s)
        else
            offset = state.loc.offset
            for i = 1:length(x.args[2].args)
                if is_for_iter(x.args[2].args[i])
                    assign_to_tuple(x.args[2].args[i].arg1, x.args[2].args[i], offset, state, s)
                end
                offset += x.args[2].args[i].fullspan
            end
        end
    elseif x isa CSTParser.EXPR{CSTParser.Do}
        for arg in CSTParser.flatten_tuple(x.args[3])
            name = CSTParser.str_value(CSTParser.get_name(arg))
            add_binding(name, x, state, s)
        end
    elseif x isa CSTParser.EXPR{CSTParser.Generator}
        if is_for_iter(x.args[3])
            offset = state.loc.offset
            for i = 3:length(x.args)
                if is_for_iter(x.args[i])
                    assign_to_tuple(x.args[i].arg1, x.args[i].arg2, offset, state, s)
                end
                offset += x.args[i].fullspan
            end
        elseif x.args[3] isa CSTParser.EXPR{CSTParser.Filter}
            offset = state.loc.offset
            for i = 1:length(x.args[3].args)
                if is_for_iter(x.args[3].args[i])
                    assign_to_tuple(x.args[3].args[i].arg1, x.args[3].args[i].arg2, offset, state, s)
                end
                offset += x.args[3].args[i].fullspan
            end
        end
    elseif CSTParser.defines_anon_function(x)
        for arg in CSTParser.flatten_tuple(x.arg1)
            name = CSTParser.str_value(CSTParser.get_name(arg))
            add_binding(name, x, state, s)
        end
    elseif x isa CSTParser.WhereOpCall
        offset = state.loc.offset + x.arg1.fullspan + x.op.fullspan
        for arg in x.args
            if !(arg isa CSTParser.PUNCTUATION) 
                #TODO: add subtype marker
                arg1 = CSTParser.rem_curly(CSTParser.rem_subtype(arg))
                s.bindings += 1
                val = Binding(Location(state.loc.file, offset), SIndex(s.index, s.bindings), arg1, state.server.packages["Core"].vals["DataType"])
                add_binding(CSTParser.str_value(arg1), val, state.bindings, s.index)
            end
            offset += arg.fullspan
        end 
    elseif (x isa CSTParser.BinarySyntaxOpCall && x.op.kind == CSTParser.Tokens.EQ && x.arg1 isa CSTParser.EXPR{CSTParser.Curly})
        offset = state.loc.offset + x.arg1.args[1].fullspan
        for i = 2:length(x.arg1.args)
            arg = x.arg1.args[i]
            if !(arg isa CSTParser.PUNCTUATION) 
                #TODO: add subtype marker
                arg1 = CSTParser.rem_curly(CSTParser.rem_subtype(arg))
                s.bindings += 1
                val = Binding(Location(state.loc.file, offset), SIndex(s.index, s.bindings), arg1, state.server.packages["Core"].vals["DataType"])
                add_binding(CSTParser.str_value(arg1), val, state.bindings, s.index)
            end
            offset += arg.fullspan
        end
    end
end

function assign_to_tuple(x::CSTParser.EXPR{CSTParser.InvisBrackets}, val, offset, state, s)
    assign_to_tuple(x.args[2], val, offset + x.args[1].fullspan, state, s)
    return offset + x.fullspan
end

function assign_to_tuple(x, val, offset, state, s)
    if x isa CSTParser.EXPR{CSTParser.TupleH}
        for arg in x
            if !(arg isa CSTParser.PUNCTUATION)
                offset = assign_to_tuple(arg, val, offset, state, s)
            else
                offset += arg.fullspan
            end
        end
    else
        name = CSTParser.str_value(CSTParser.rem_decl(x))
        s.bindings += 1
        b = Binding(Location(state.loc.file, offset), SIndex(s.index, s.bindings), val, nothing)
        add_binding(name, b, state.bindings, s.index)
        
        offset += x.fullspan
    end
    return offset
end


function is_for_iter(x)
    (x isa CSTParser.BinarySyntaxOpCall || x isa CSTParser.BinaryOpCall) && x.op.kind in (CSTParser.Tokens.IN, CSTParser.Tokens.ELEMENT_OF, CSTParser.Tokens.EQ)
end

function get_iter_args(x::CSTParser.BinarySyntaxOpCall, state, s)
    name = CSTParser.str_value(CSTParser.get_name(x.args[3].args[i].arg1))
    add_binding(name, x, state, s)
end

function get_fcall_bindings(sig, state, s)
    offset = state.loc.offset
    while sig isa CSTParser.WhereOpCall
        offset1 = offset + sig.arg1.fullspan + sig.op.fullspan
        for arg in sig.args
            if !(arg isa CSTParser.PUNCTUATION)
                #TODO: add subtype marker
                arg1 = CSTParser.rem_curly(CSTParser.rem_subtype(arg))
                s.bindings += 1
                val = Binding(Location(state.loc.file, offset1), SIndex(s.index, s.bindings), arg1, state.server.packages["Core"].vals["DataType"])
                add_binding(CSTParser.str_value(arg1), val, state.bindings, s.index)
            end
            offset1 += arg.fullspan
        end        
        sig = sig.arg1
    end
    if sig isa CSTParser.BinarySyntaxOpCall && CSTParser.is_decl(sig.op)
        sig = sig.arg1
    end
    
    sig isa CSTParser.IDENTIFIER && return 
    if sig isa CSTParser.EXPR{CSTParser.Call}
        if sig.args[1] isa CSTParser.EXPR{CSTParser.InvisBrackets} && length(sig.args[1].args) == 3 && sig.args[1].args[2] isa CSTParser.BinarySyntaxOpCall && sig.args[1].args[2].op.kind == CSTParser.Tokens.DECLARATION
            dtname = CSTParser.rem_decl(sig.args[1].args[2])
            s.bindings += 1
                val = Binding(Location(state.loc.file, offset + sig.args[1].args[1].fullspan), SIndex(s.index, s.bindings), dtname, state.server.packages["Core"].vals["DataType"])
                add_binding(CSTParser.str_value(dtname), val, state.bindings, s.index)
        end
        if sig.args[1] isa CSTParser.EXPR{CSTParser.Curly}
            offset1 = offset + sig.args[1].args[1].fullspan
            for i = 2:length(sig.args[1].args)
                arg = sig.args[1].args[i]
                if !(arg isa CSTParser.PUNCTUATION)
                    #TODO: add subtype marker
                    arg1 = CSTParser.rem_subtype(arg)
                    s.bindings += 1
                    val = Binding(Location(state.loc.file, offset1), SIndex(s.index, s.bindings), arg1, state.server.packages["Core"].vals["DataType"])
                    add_binding(CSTParser.str_value(arg1), val, state.bindings, s.index)
                end
                offset1 += arg.fullspan
            end
        end
    end
    !(sig isa CSTParser.EXPR) || length(sig.args) < 3 && return 
    if sig isa CSTParser.EXPR && length(sig.args) > 2
        offset += sig.args[1].fullspan + sig.args[2].fullspan
        for i = 3:length(sig.args)-1
            arg = sig.args[i]
            if !(arg isa CSTParser.PUNCTUATION) 
                get_arg_type(arg, state, s, offset)
            end
            offset += arg.fullspan
        end
    elseif sig isa CSTParser.UnaryOpCall
        offset += sig.op.fullspan
        if sig.arg isa CSTParser.EXPR{CSTParser.InvisBrackets}
            offset += sig.arg.args[1].fullspan
            for i = 2:length(sig.arg.args)-1
                arg = sig.arg.args[i]
                if !(arg isa CSTParser.PUNCTUATION) 
                    get_arg_type(arg, state, s, offset)
                end
                offset += arg.fullspan
            end
        else
            get_arg_type(sig.arg, state, s, offset)
        end
    elseif sig isa CSTParser.BinaryOpCall || sig isa CSTParser.BinarySyntaxOpCall
        get_arg_type(sig.arg1, state, s, offset)
        offset += sig.arg1.fullspan + sig.op.fullspan
        get_arg_type(sig.arg2, state, s, offset)
    end
end


function get_arg_type(arg, state, s, offset)
    if arg isa CSTParser.UnarySyntaxOpCall && CSTParser.is_dddot(arg.arg2)
        arg = arg.arg1
    end
    if arg isa CSTParser.IDENTIFIER
        t = nothing
    elseif arg isa CSTParser.BinarySyntaxOpCall && CSTParser.is_decl(arg.op)
        if arg.arg1 isa CSTParser.EXPR{CSTParser.TupleH}
            return get_arg_type(arg.arg1, state, s, offset)
        end
        t = arg.arg2
        arg = arg.arg1
    elseif arg isa CSTParser.EXPR{CSTParser.Kw}
        if arg.args[1] isa CSTParser.BinarySyntaxOpCall && CSTParser.is_decl(arg.args[1].op)
            t = arg.args[1].arg2
            arg = arg.args[1].arg1
        else arg.args[1] isa CSTParser.IDENTIFIER
            arg = arg.args[1]
            t = nothing
        end
    else
        t = nothing
    end
    s.bindings += 1
    val = Binding(Location(state.loc.file, offset), SIndex(s.index, s.bindings), arg, t)
    add_binding(CSTParser.str_value(arg), val, state.bindings, s.index)
    return
end

function get_arg_type(arg::CSTParser.EXPR{CSTParser.TupleH}, state, s, offset)
    t = nothing
    for a in arg.args
        if a isa CSTParser.IDENTIFIER
            s.bindings += 1
            val = Binding(Location(state.loc.file, offset), SIndex(s.index, s.bindings), a, t)
            add_binding(CSTParser.str_value(a), val, state.bindings, s.index)
        end
        offset += a.fullspan
    end
end

function get_arg_type(arg::CSTParser.EXPR{CSTParser.Parameters}, state, s, offset)
    for a in arg.args
        get_arg_type(a, state, s, offset)
        offset += a.fullspan
    end
end


function get_struct_bindings(x, state, s)
    isstruct = x isa CSTParser.EXPR{CSTParser.Struct}
    sig = CSTParser.get_sig(x)
    sig = CSTParser.rem_subtype(sig)
    sig = CSTParser.rem_where(sig)
    for arg in CSTParser.get_curly_params(sig)
        add_binding(arg, x, state, s, state.server.packages["Core"].vals["DataType"])
    end
    for arg in x.args[isstruct ? 3 : 4]
        if !CSTParser.defines_function(arg)
            if arg isa CSTParser.BinarySyntaxOpCall && CSTParser.is_decl(arg.op)
                name = CSTParser.str_value(arg.arg1)
                t = arg.arg2
            else
                name = CSTParser.str_value(arg)
                t = nothing
            end
            add_binding(name, x, state, s, t)
        end
    end
end

function _store_search(strs, store, i = 1, bs = [])
    if haskey(store, strs[i])
        push!(bs, store[strs[i]])
        if i == length(strs)
            return bs
        else
            return _store_search(strs, store[strs[i]], i+1, bs)
        end
    else
        return nothing
    end
end

function get_imports(x, state, s)
    s.bindings += 1
    push!(state.imports, ImportBinding(Location(state), SIndex(s.index, s.bindings), x))
end

function cat_bindings(file, vars = State("", file.state.server))
    for (ind, d) in file.state.bindings
        if !haskey(vars.bindings, ind)
            vars.bindings[ind] = Dict{Tuple,Dict{String,Vector{Binding}}}()
        end
        for (n, bs) in file.state.bindings[ind]
            if !haskey(vars.bindings[ind], n)
                vars.bindings[ind][n] = Binding[]
            end
            append!(vars.bindings[ind][n], file.state.bindings[ind][n])
        end
    end
    
    append!(vars.modules.list, file.state.modules.list)
    union!(vars.modules.names, file.state.modules.names)
    append!(vars.imports, file.state.imports)
    
    for (name,bs) in file.state.exports
        if !haskey(vars.exports, name)
            vars.exports[name] = []
        end
        append!(vars.exports[name], bs)
    end
    for m in file.state.used_modules
        push!(vars.used_modules, m)
    end
    
    
    for incl in file.state.includes
        if is_loaded(file.state.server, incl.file)
            cat_bindings(getfile(file.state.server, incl.file), vars)
        else
            @warn "Failed to include $(incl.file)"
        end
    end
    return vars
end



function build_bindings(file)
    state = cat_bindings(file)
    # add imports
    state.used_modules = [
        Binding(Location(file.state), SIndex(file.index, file.nb), file.state.server.packages["Base"], file.state.server.packages["Core"].vals["Module"]),
        Binding(Location(file.state), SIndex(file.index, file.nb), file.state.server.packages["Core"], file.state.server.packages["Core"].vals["Module"])]
    resolve_imports(state)
    return state
end

# find binding in specific scope index
function find_binding(bindings, name, ind, st::Function)
    out = Binding[]
    if haskey(bindings, ind) && haskey(bindings[ind], name)
        for b in bindings[ind][name]
            if st(b)
                push!(out, b)
            end
        end
    end
    return out
end

# as above but ascend scopes
function find_binding_ascend(bindings, name, ind, st::Function)
    out = []
    while length(ind) > 0
        append!(out, find_binding(bindings, name, ind, st))
        ind = shrink_tuple(ind)
    end
    out
end


function _get_field(par, arg, state)
    if par isa Dict #package store
        if haskey(par, CSTParser.str_value(arg))
            par = par[CSTParser.str_value(arg)]
            if par isa String # reference to dependency
                if haskey(state.server.packages, par)
                    return state.server.packages[par] 
                else
                    return 
                end
            end
            return par
        else
            return
        end
    elseif par isa SymbolServer.ModuleStore # imported module
        if haskey(par.vals, CSTParser.str_value(arg))
            par = par.vals[CSTParser.str_value(arg)]
            if par isa String # reference to dependency
                if haskey(state.server.packages, par)
                    return state.server.packages[par] 
                else
                    return 
                end
            end
            return par
        else
            return
        end
    elseif par isa Tuple
        ret = find_binding(state.bindings, CSTParser.str_value(arg), par, b-> b.val isa CSTParser.EXPR{T} where T <: Union{CSTParser.ModuleH,CSTParser.BareModule}) 
        if isempty(ret)
            return
        else
            par = last(ret)
            return par
        end
    elseif par isa Binding
        ind = add_to_tuple(par.si.i, par.si.n + 1)
        ret = find_binding(state.bindings, CSTParser.str_value(arg), ind, x->true) 
        if isempty(ret)
            return
        else
            par = last(ret)
            return par
        end
    end
    return
end



function resolve_import(imprt, state)
    x = imprt.val
    u = x isa CSTParser.EXPR{CSTParser.Using}
    i = 2
    n = length(x.args)
    argname = ""
    predots = 0
    
    root = par = state.server.packages
    bindings = []
    while i <= length(x.args)
        arg = x.args[i]
        if arg isa CSTParser.IDENTIFIER
            par = _get_field(par,arg, state)
            argname = CSTParser.str_value(x.args[i])
            if par == nothing
                return
            end
        elseif arg isa CSTParser.PUNCTUATION && arg.kind == CSTParser.Tokens.COMMA
            push!(bindings, (true, argname, par))
            par = root
        elseif arg isa CSTParser.OPERATOR && arg.kind == CSTParser.Tokens.COLON
            root = par
            push!(bindings, (false, argname, par))
        elseif arg isa CSTParser.PUNCTUATION && arg.kind == CSTParser.Tokens.DOT
            #dot between identifiers
            push!(bindings, (false, argname, par))
        elseif arg isa CSTParser.OPERATOR && arg.kind == CSTParser.Tokens.DOT
            #dot prexceding identifier
            if par == root == state.server.packages
                par = shrink_tuple(imprt.si.i)
            elseif par isa Tuple
                if length(par) > 0
                    par = shrink_tuple(par)
                else
                    return
                end
            else
                return
            end
        else
            return
        end
        if i == n
            push!(bindings, (true, CSTParser.str_value(arg), par))
        end
        i += 1
    end
    ext_si =  SIndex(shrink_tuple(imprt.si.i), last(imprt.si.i))
    for b in bindings
        # b contains (doimport, name, val)
        if !b[1]
            if b[3] isa Binding
                binding = Binding(imprt.loc, imprt.si, b[3].val, b[3].t)
            else
                binding = Binding(imprt.loc, imprt.si, b[3], nothing)
            end
            add_binding(b[2], binding, state.bindings, imprt.si.i)
            continue
        end
        if b[3] isa Binding
            binding = Binding(imprt.loc, ext_si, b[3].val, b[3].t)
        else
            binding = Binding(imprt.loc, ext_si, b[3], nothing)
        end
        add_binding(b[2], binding, state.bindings, ext_si.i)
        
        if u 
            if b[3] isa SymbolServer.ModuleStore
                push!(state.used_modules,binding)
            elseif b[3] isa Binding
                ind = add_to_tuple(b[3].si.i, b[3].si.n + 1)                
                if haskey(state.exports, ind)
                    for n in state.exports[ind]
                        ret = find_binding(state.bindings, n, ind, x-> true)
                        isempty(ret) && continue
                        eb = last(ret)
                        if eb isa Binding
                            binding = Binding(imprt.loc, ext_si, eb.val, eb.t)
                        else
                            binding = Binding(imprt.loc, ext_si, eb, nothing)
                        end
                        add_binding(n, binding, state.bindings, ext_si.i)
                    end
                end
            end
        end
    end
end

function resolve_imports(state)
    for imprt in state.imports
        resolve_import(imprt, state)
    end
end